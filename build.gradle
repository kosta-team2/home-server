plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.ec4j.editorconfig' version '0.1.0'
    id 'checkstyle'
    id 'jacoco'
}

group = 'com.home'
version = '0.0.1-SNAPSHOT'
description = 'home-server'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

editorconfig {
    excludes = ['build', '.gradle', 'out', 'node_modules']
}
check.dependsOn editorconfigCheck

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:4.0.0'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:4.0.0'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

tasks.named('test') {
    useJUnitPlatform()
    outputs.dir snippetsDir
    //mokito 경고 무시
    jvmArgs("-XX:+EnableDynamicAgentLoading",
            "-Xshare:off"
    )
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'BOOT-INF/classes/static/docs'
    }
}

// 한 방 검증 파이프라인(이름: verify)
tasks.register('verify') {
    group = 'verification'
    description = 'Run editorconfig, checkstyle, unit tests, JaCoCo report & coverage verification'
    dependsOn 'editorconfigCheck',
            'checkstyleMain', 'checkstyleTest',
            'test',
            'jacocoTestReport',
            'jacocoTestCoverageVerification'
}

//check 시 커버리지 검증 포함
check {
    dependsOn jacocoTestCoverageVerification
}

//build로 체킹
checkstyle {
    maxWarnings = 0 // 규칙이 어긋나는 코드가 하나라도 있을 경우 빌드 fail을 내고 싶다면 이 선언을 추가
    configFile = file("${rootDir}/config/code-style/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/config/code-style/naver-checkstyle-suppressions.xml"]
    toolVersion = "10.20.0"  // checkstyle 버전 8.24 이상 선언
}

jacoco {
    toolVersion = "0.8.13"
}

// === JaCoCo 공통 exclude 패턴 ===
def jacocoExcludes = [
        '**/Q*',              // QueryDSL
        '**/*Application*',   // 메인 애플리케이션 클래스
        '**/*Config*',        // 설정 클래스
        '**/dto/**',          // DTO
]

// 리포트 생성
jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }

    // 리포트 대상 클래스에서 공통 exclude 적용
    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: jacocoExcludes)
            })
    )
}

// 커버리지 검증
jacocoTestCoverageVerification {
    dependsOn test

    // 검증 대상 클래스에서도 동일하게 공통 exclude 적용
    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: jacocoExcludes)
            })
    )

    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value   = 'COVEREDRATIO'
                minimum = 0.70    // 라인 커버리지 최소 70%
            }
        }
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value   = 'COVEREDRATIO'
                minimum = 0.70    // 브랜치 커버리지 최소 70%
            }
        }
    }
}
