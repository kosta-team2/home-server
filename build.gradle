plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'com.epages.restdocs-api-spec' version '0.19.4'
    id 'org.ec4j.editorconfig' version '0.1.0'
    id 'checkstyle'
    id 'jacoco'
}

group = 'com.home'
version = '0.0.1-SNAPSHOT'
description = 'home-server'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

editorconfig {
    excludes = ['build', '.gradle', 'out', 'node_modules']
}
check.dependsOn editorconfigCheck

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-batch'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.2'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.2'

    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.4'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

tasks.named('test') {
    useJUnitPlatform()
    //mokito 경고 무시
    jvmArgs("-XX:+EnableDynamicAgentLoading",
            "-Xshare:off"
    )
}

//@Tag("restDocs")을 찾아서 문서화
tasks.register("restDocsTest", Test) {
    dependsOn tasks.named("test")
    outputs.dir snippetsDir

    useJUnitPlatform {
        includeTags("restDocs")
    }

    finalizedBy "asciidoctor"
    finalizedBy "openapi3"
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn tasks.named('test')
    dependsOn tasks.named('restDocsTest')
}

openapi3 {
    server = 'http://localhost:8080'
    title = 'Home API'
    description = '실거래가 비교 프로젝트 백엔드 API'
    version = project.version.toString()
    format = 'yaml'
}


bootJar {
//    dependsOn 'asciidoctor', 'openapi3'
//
//    from("swagger-ui") {
//        into "BOOT-INF/classes/static/swagger"
//    }
//
//    from("${asciidoctor.outputDir}") {
//        into 'BOOT-INF/classes/static/docs'
//    }
//
//    from(layout.buildDirectory.dir("api-spec")) {
//        into 'BOOT-INF/classes/static/swagger'
//    }
}

// 한 방 검증 파이프라인(이름: verify)
tasks.register('verify') {
    group = 'verification'
    description = 'Run editorconfig, checkstyle, unit tests, JaCoCo report & coverage verification'
    dependsOn 'editorconfigCheck',
            'checkstyleMain', 'checkstyleTest',
            'test',
            'jacocoTestReport',
            'jacocoTestCoverageVerification'
}

//check 시 커버리지 검증 포함
check {
    dependsOn jacocoTestCoverageVerification
}

//build로 체킹
checkstyle {
    maxWarnings = 0 // 규칙이 어긋나는 코드가 하나라도 있을 경우 빌드 fail을 내고 싶다면 이 선언을 추가
    configFile = file("${rootDir}/config/code-style/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/config/code-style/naver-checkstyle-suppressions.xml"]
    toolVersion = "10.20.0"  // checkstyle 버전 8.24 이상 선언
}

jacoco {
    toolVersion = "0.8.13"
}

// === JaCoCo 공통 exclude 패턴 ===
def jacocoExcludes = [
        '**/Q*',                 // QueryDSL
        '**/*Application*',      // 메인 애플리케이션 클래스
        '**/*Config*',           // 설정 클래스
        '**/dto/**',             // DTO
        '**/external/**',        // API Client
        '**/global/exception/**',// exception 패키지 내부
        '**/*Exception*',        // exception 클래스
        '**/*Controller*'        // controller 클래스
]

// 리포트 생성
jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }

    // 리포트 대상 클래스에서 공통 exclude 적용
    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: jacocoExcludes)
            })
    )
}

// 커버리지 검증
jacocoTestCoverageVerification {
    dependsOn test

    // 검증 대상 클래스에서도 동일하게 공통 exclude 적용
    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: jacocoExcludes)
            })
    )

    violationRules {
/*
          rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.70    // 라인 커버리지 최소 70%
            }
        }
*/
/*
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.70    // 브랜치 커버리지 최소 70%
            }
        }
*/
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.70  // 전체 평균 70% 유지
            }
        }
    }
}
